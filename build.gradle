apply plugin:'groovy'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
  mavenCentral()
  maven {
    name 'clojars.org'
    url  'http://clojars.org/repo'
  }
  maven {
    name 'cloudera'
    url  'http://repository.cloudera.com/artifactory/cloudera-repos'
  }
}

configurations {
  provided
  compile.extendsFrom provided
}

dependencies {
  compile group:'org.codehaus.groovy', name:'groovy',        version:'2.2.2'
  compile(group:'org.apache.hadoop',   name:'hadoop-common', version:'2.0.0-cdh4.4.0') {
    exclude group:'org.slf4j', module:'slf4j-log4j12'
  }
  compile(group:'org.apache.hadoop',   name:'hadoop-hdfs',   version:'2.0.0-cdh4.4.0') {
    // http://wlangiewicz.com/2013/02/08/problem-with-commons-daemon-1-0-3-in-maven-central-repository/
    exclude group:'commons-daemon', module:'commons-daemon'
  }
  compile(group:'org.apache.hbase', name:'hbase', version:'0.94.6-cdh4.4.0') {
    exclude group:'org.slf4j', module:'slf4j-log4j12'
  }

  provided group:'storm',               name:'storm',         version:'0.9.0.1'
}

jar {
  from {
    (configurations.runtime - configurations.provided).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
}

task wrapper(type:Wrapper) {
  gradleVersion = '1.11'
}

// vim: ft=groovy
